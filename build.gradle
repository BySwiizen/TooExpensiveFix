import org.apache.tools.ant.filters.ReplaceTokens


plugins {
    id 'java'
    // https://github.com/Goooler/shadow
    id 'io.github.goooler.shadow' version '8.1.7'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group 'com.budderman18.tooexpensivefix'
version '6.0'

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.encoding = 'UTF-8'
}


repositories {
    mavenCentral()

    // Sonatype
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    // SpigotMC
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation 'org.jetbrains:annotations:24.1.0'
    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot/browse
    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
}


shadowJar {
    archiveFileName = project.name + "-" + project.version + ".jar"
    relocate 'org.bstats', 'com.budderman18.tooexpensivefix.metrics'
    exclude 'META-INF/**', 'LICENSE'
}

build {
    dependsOn(shadowJar)
}


bukkit {
    name = 'TooExpensiveFix'
    main = 'com.budderman18.tooexpensivefix.TooExpensiveFix'
    description = 'Plugin that removes the "Too Expensive!" during repair and which fixes all item renamings to 1 level'
    version = '@VERSION@'
    apiVersion = '1.13'
    authors = ['datatags', 'BySwiizen']
}

processResources {
    filter ReplaceTokens, tokens: ['VERSION': project.version]
}